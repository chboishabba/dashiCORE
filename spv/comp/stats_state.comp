#version 450
layout(local_size_x = 256) in;

layout(std430, set=0, binding=0) readonly buffer X { float x[]; };
layout(std430, set=0, binding=1) writeonly buffer P { vec4 partials[]; };
// partials[group] = (min, max, sum, sumsq)

layout(push_constant) uniform PC { uint n; } pc;

shared float sh_min[256];
shared float sh_max[256];
shared float sh_sum[256];
shared float sh_sumsq[256];

void main() {
    uint gid = gl_GlobalInvocationID.x;
    uint lid = gl_LocalInvocationID.x;

    float v = 0.0;
    bool ok = gid < pc.n;
    if (ok) v = x[gid];

    sh_min[lid] = ok ? v : 3.402823e38;  // +FLT_MAX
    sh_max[lid] = ok ? v : -3.402823e38; // -FLT_MAX
    sh_sum[lid] = ok ? v : 0.0;
    sh_sumsq[lid] = ok ? v*v : 0.0;
    barrier();

    for (uint stride = 128; stride > 0; stride >>= 1) {
        if (lid < stride) {
            sh_min[lid] = min(sh_min[lid], sh_min[lid + stride]);
            sh_max[lid] = max(sh_max[lid], sh_max[lid + stride]);
            sh_sum[lid] += sh_sum[lid + stride];
            sh_sumsq[lid] += sh_sumsq[lid + stride];
        }
        barrier();
    }

    if (lid == 0) {
        partials[gl_WorkGroupID.x] = vec4(sh_min[0], sh_max[0], sh_sum[0], sh_sumsq[0]);
    }
}
