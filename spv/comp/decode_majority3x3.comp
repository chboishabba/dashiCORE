#version 450

// 3x3 periodic majority over ternary sign grid.

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(std430, set = 0, binding = 0) readonly buffer SignIn {
    int data[];
} sign_in;

layout(std430, set = 0, binding = 1) buffer SignOut {
    int data[];
} sign_out;

layout(push_constant) uniform PushConsts {
    uint N;
} pc;

void main() {
    uint x = gl_GlobalInvocationID.x;
    uint y = gl_GlobalInvocationID.y;
    if (x >= pc.N || y >= pc.N) {
        return;
    }
    uint idx = y * pc.N + x;

    int vote = 0;
    // periodic wrap
    for (int dy = -1; dy <= 1; ++dy) {
        int yy = (int(y) + dy + int(pc.N)) % int(pc.N);
        for (int dx = -1; dx <= 1; ++dx) {
            int xx = (int(x) + dx + int(pc.N)) % int(pc.N);
            uint nidx = uint(yy) * pc.N + uint(xx);
            vote += sign_in.data[nidx];
        }
    }

    int s = 0;
    if (vote > 0) {
        s = 1;
    } else if (vote < 0) {
        s = -1;
    }
    sign_out.data[idx] = s;
}
